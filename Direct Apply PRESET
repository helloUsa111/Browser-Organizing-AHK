;---------------------------------------------Auto apply preset()-----------------------------------

^F5:: ;reframe and blur
preset("reframe")
sleep 100
preset("blur 150")
return

^F6::preset("Zoomin35")
^F7::preset("Zoomout35")
^F8::preset("Top-Bottom")
^F9::preset("BottomTop")
^F10::preset("Right-Left")
^F11::preset("Left-Right")


#IfWinActive ahk_exe Adobe Premiere Pro.exe
preset(item)
{

;Keyshower(item,"preset") ;YOU DO NOT NEED THIS LINE. -- it simply displays keystrokes on the screen for the sake of tutorials... IF the function "keyshower" has been defined.
if IsFunc("Keyshower") {
	Func := Func("Keyshower")
	RetVal := Func.Call(item,"preset") 
}

ifWinNotActive ahk_exe Adobe Premiere Pro.exe
	goto theEnding ;and this line is here just in case the function is called while not inside premiere.
Sendinput, ^!+k 
sleep 25
Send ^!+k ; another shortcut for Shuttle Stop. CTRL ALT SHIFT K. Set this in Premiere's shortcuts panel.
sleep 25
coordmode, pixel, Window
coordmode, mouse, Window
coordmode, Caret, Window

BlockInput, SendAndMouse
BlockInput, MouseMove
BlockInput, On

SetKeyDelay, 0 ;this ensures that any text AutoHotKey "types in," will input instantly, rather than one letter at a time.
MouseGetPos, xposP, yposP ;---stores the cursor's current coordinates at X%xposP% Y%yposP%
send, {mButton} ;this will MIDDLE CLICK to bring focus to the panel underneath the cursor (the timeline). I forget exactly why, but if you create a nest, and immediately try to apply a preset to it, it doesn't work, because the timeline wasn't in focus...?
Send ^+!7 ;CTRL SHIFT ALT 7 - "effects" panel
sleep 25
Send ^b ;CTRL B -- set in premiere to "select find box"
sleep 25
MouseMove, %A_CaretX%, %A_CaretY%, 0
sleep 25
MouseMove, %A_CaretX%, %A_CaretY%, 0
sleep 25

MouseGetPos, , , Window, classNN
WinGetClass, class, ahk_id %Window%
;msgbox, ahk_class =   %class% `nClassNN =     %classNN% `nTitle= %Window%
 
ControlGetPos, XX, YY, Width, Height, %classNN%, ahk_class %class%, SubWindow, SubWindow 

MouseMove, XX-290, YY+5, 0 ;-----------------------moves cursor onto the magnifying glass
;msgbox, should be in the center of the magnifying glass now.
sleep 25
Send %item%

sleep 50
MouseMove, 50, 65, 0, R ;
;msgbox, The cursor should be directly on top of the preset's icon. `n If not, the script needs modification.

sleep 25
MouseGetPos, iconX, iconY, Window, classNN ;---now we have to figure out the ahk_class of the current panel we are on. It used to be DroverLord - Window Class14, but the number changes anytime you move panels around... so i must always obtain the information anew.
sleep 25
WinGetClass, class, ahk_id %Window% ;----------"ahk_id %Window%" is important for SOME REASON. if you delete it, this doesnt work.
ControlGetPos, xxx, yyy, www, hhh, %classNN%, ahk_class %class%, SubWindow, SubWindow 
MouseMove, www/4, hhh/2, 0, R 
sleep 25
MouseClick, left, , , 1 ;-----------------------the actual click
sleep 25
MouseMove, iconX, iconY, 0 ;--------------------moves cursor BACK onto the effect's icon
sleep 35
MouseClickDrag, Left, , , %xposP%, %yposP%, 0 
sleep 25
MouseClick, middle, , , 1
blockinput, MouseMoveOff ;returning mouse movement ability
BlockInput, off 


theEnding:
}
;---------------------------------------------END of preset()-----------------------------------
